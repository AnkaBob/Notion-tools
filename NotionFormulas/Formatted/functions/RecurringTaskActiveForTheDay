and(
    # Testing it is not deactivated 
    !prop("Settings").contains("Deactivated"),
    # Testing if work mode and work mode is on
    or(
        and(
            prop("Configuration database").first().prop("Current day setting").contains("Work"),
            prop("Settings").contains("Work")
        ),
        !prop("Settings").contains("Work")
    ),

    # Testing weekdays and weekends
    or(
        !prop("Weekday only"), 
        !contains("Saturday,Sunday", formatDate(prop("Configuration database").first().prop("Current day start"), "dddd"))
    ),
    or(
        !prop("Weekend only"),
        contains("Saturday,Sunday", formatDate(prop("Configuration database").first().prop("Current day start"), "dddd"))
    ),

    # Testing for days of the month (for instance if a task is only after the 15th of the month or before the 5th of the month)
    # Filter on particular days of the month
    or(
        empty(prop("Starting day of the month")),
        prop("Starting day of the month") <= date(prop("Configuration database").first().prop("Current day start"))
    ),
    or(
        empty(prop("Ending day of the month")),
        prop("Ending day of the month") >= date(prop("Configuration database").first().prop("Current day start"))
    ),

    # Testing for hours of the day (for instance if a task is only after 9am or before 5pm)
    or(
        empty(prop("Starting hour")),
        prop("Starting hour") <= hour(now())
    ),
    or(
        empty(prop("Ending hour")),
        prop("Ending hour") >= hour(now())
    ),

    # Testing for days of the week (for instance if a task is only on Mondays)
    or(
        empty(prop("Day of the week (only)")),
        prop("Day of the week (only)") == formatDate(prop("Configuration database").first().prop("Current day start"), "dddd")
    )
)