and(
    # Testing if work mode and work mode is on
    or(
        and(
            prop("Configuration database").first().prop("Work"),
            prop("Settings").contains("Work")
        ),
        and(
            !prop("Configuration database").first().prop("Work"),
            !prop("Settings").contains("Work")
        )
    ),

    # Testing weekdays and weekends
    or(
        !prop("Weekday only"), 
        !contains("Saturday,Sunday", formatDate(now(), "dddd"))
    ),
    or(
        !prop("Weekend only"),
        contains("Saturday,Sunday", formatDate(now(), "dddd"))
    ),

    # Testing for days of the month (for instance if a task is only after the 15th of the month or before the 5th of the month)
    # Filter on particular days of the month
    or(
        empty(prop("Starting day of the month")),
        prop("Starting day of the month") <= date(now())
    ),
    or(
        empty(prop("Ending day of the month")),
        prop("Ending day of the month") >= date(now())
    ),

    # Testing for hours of the day (for instance if a task is only after 9am or before 5pm)
    or(
        empty(prop("Starting hour")),
        prop("Starting hour") <= hour(now())
    ),
    or(
        empty(prop("Ending hour")),
        prop("Ending hour") >= hour(now())
    ),

    # Testing for days of the week (for instance if a task is only on Mondays)
    or(
        empty(prop("Day of the week (only)")),
        prop("Day of the week (only)") == formatDate(now(), "dddd")
        #Below code cause issues when we don't have last done time, 
        #it can be overcome by added a 1 day interval to the last done time in the settings
        #
        #and(
        #    prop("Day of the week (only)") == formatDate(now(), "dddd"),
        #    or(
        #        empty(prop("Last done time")),
        #        formatDate(now(), "M/D/YY") > formatDate(prop("Last done time"), "M/D/YY")
        #    )
        #)
    )
)



#We need to check if days since last done is empty, if so we show the task
#Should not be needed because the default value is 999999
#Add the skipped day information
#It seems that the fomula don't work at all if the last_done is empty, maybe we can move the test at the beginning of the formula